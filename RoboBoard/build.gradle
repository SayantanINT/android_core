buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
        classpath 'org.robolectric:robolectric-gradle-plugin:0.12.0'
    }
}
apply plugin: 'android'
apply plugin: 'robolectric'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 4
        versionName "2.2.0"
    }

    dexOptions {
        preDexLibraries = false
    }

    if(project.hasProperty("RobotMitya.properties")
            && new File(project.property("RobotMitya.properties")).exists()) {

        Properties props = new Properties()
        props.load(new FileInputStream(file(project.property("RobotMitya.properties"))))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        /* https://github.com/rosjava/android_core/issues/194 */
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile 'org.ros.android_core:android_honeycomb_mr2:0.1.+'
    compile 'org.ros.rosjava_core:rosjava:0.1.+'
    compile 'com.android.support:appcompat-v7:19.1.0'
    androidTestCompile 'junit:junit:4.5+'
    compile project(":RoboCommonLib")
    compile 'com.intellij:annotations:12.0'
}
